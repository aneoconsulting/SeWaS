/*
  SeWaS
  Copyright (C) 2018  ANEO

  This program is free software: you can redistribute it and/or modify
  it under the terms of the GNU Affero General Public License as published
  by the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU Affero General Public License for more details.

  You should have received a copy of the GNU Affero General Public License
  along with this program.  If not, see <http://www.gnu.org/licenses/>.
==============================================================================*/

extern "C" %{
  /* Prototypes of the wrappers to be called by PaRSEC' tasks */
  typedef void (*initializeFields_t   )(const int ii, const int jj, const int kk);

  typedef int  (*computeVelocity_t    )(const int d,  const int ts, const int ii, const int jj, const int kk);
  typedef void (*extractVelocityHalo_t)(const int l, const int d,  const int ts, const int ii, const int jj, const int kk, void * vH);
  typedef void (*updateVelocity_t     )(const int l, const int d,  const int ts, const int ii, const int jj, const int kk, void * vH);

  typedef int  (*computeStress_t      )(const int sc, const int ts, const int ii, const int jj, const int kk);
  typedef void (*extractStressHalo_t  )(const int l, const int sc, const int ts, const int ii, const int jj, const int kk, void * sigmaH);
  typedef void (*updateStress_t       )(const int l, const int sc, const int ts, const int ii, const int jj, const int kk, void * sigmaH);

  typedef void (*displayVelocity_t    )(const int d,  const int ts, const int ii, const int jj, const int kk, const int tid);
  typedef void (*displayStress_t      )(const int sc, const int ts, const int ii, const int jj, const int kk, const int tid);

  /* Priority evaluator */
  typedef int  (*getPriority_t        )(const int taskType, const int ts, const int ii, const int jj, const int kk);

#include "constants.h"
%}



/* Directions */
X				[type = "int" hidden = on default = X]
Y				[type = "int" hidden = on default = _Y]
Z				[type = "int" hidden = on default = _Z]
DIM				[type = "int" hidden = on default = _DIM]


/* Stress Field Components */
XX				[type = "int" hidden = on default = _XX]
YY				[type = "int" hidden = on default = _YY]
ZZ				[type = "int" hidden = on default = _ZZ]
XY				[type = "int" hidden = on default = _XY]
XZ				[type = "int" hidden = on default = _XZ]
YZ				[type = "int" hidden = on default = _YZ]
NB_STRESS_FIELD_COMPONENTS	[type = "int" hidden = on default = _NB_STRESS_FIELD_COMPONENTS]

/* Locations */
LEFT				[type = "int" hidden = on default = _LEFT]
RIGHT				[type = "int" hidden = on default = _RIGHT]
BACKWARD			[type = "int" hidden = on default = _BACKWARD]
FORWARD				[type = "int" hidden = on default = _FORWARD]
BOTTOM				[type = "int" hidden = on default = _BOTTOM]
TOP				[type = "int" hidden = on default = _TOP]
NB_LOCATIONS            	[type = "int" hidden = on default = _NB_LOCATIONS]


dim				[type = "int" hidden = on default = _DIM]				/* Dimension */
nsc				[type = "int" hidden = on default = _NB_STRESS_FIELD_COMPONENTS]	/* Number of stress field components */
nl				[type = "int" hidden = on default = _NB_LOCATIONS]			/* Total number of neighbours per domain */



/* Parameters to be passed to the PaRSEC context */
nt				[type = "int"]								/* Number of time steps */
nxx				[type = "int"]								/* Total number of blocks along x axis */
nyy				[type = "int"]								/* Total number of blocks along y axis */
nzz				[type = "int"]								/* Total number of blocks along z axis */

initializeFields                [type = "void *"]

computeVelocity			[type = "void *"]
extractVelocityHalo		[type = "void *"]
updateVelocity		        [type = "void *"]

computeStress			[type = "void *"]
extractStressHalo		[type = "void *"]
updateStress		        [type = "void *"]

displayVelocity                 [type = "void *"]
displayStress                   [type = "void *"]

getPriority                     [type = "int *"]

ddesc			        [type = "parsec_data_collection_t *"]					/* Data descriptor */







InitializeFields(ii, jj, kk)

// Execution space
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


CTL I    -> I ComputeVelocity(0..dim-1, 2, ii, jj, kk)

// Priority
; %{ return ((getPriority_t) getPriority)(INITIALIZE_FIELDS, 0, ii, jj, kk); %}


BODY
{

  ((initializeFields_t) initializeFields) (ii, jj, kk);

}
END



ComputeVelocity(d, ts, ii, jj, kk)

// Execution space
d  = 0 .. dim-1
ts = 2 .. nt-2 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
CTL I    <- (ts == 2)                      ? I    InitializeFields(ii, jj, kk)

CTL SxxH <- ( 	                   ts > 2) ? SxxH UpdateStress(XX, ts-1, ii, jj, kk)
CTL SyyH <- ( 	                   ts > 2) ? SyyH UpdateStress(YY, ts-1, ii, jj, kk)
CTL SzzH <- ( 	                   ts > 2) ? SzzH UpdateStress(ZZ, ts-1, ii, jj, kk)
CTL SxyH <- ((d == X || d == Y) && ts > 2) ? SxyH UpdateStress(XY, ts-1, ii, jj, kk)
CTL SxzH <- ((d == X || d == Z) && ts > 2) ? SxzH UpdateStress(XZ, ts-1, ii, jj, kk)
CTL SyzH <- ((d == Y || d == Z) && ts > 2) ? SyzH UpdateStress(YZ, ts-1, ii, jj, kk)

CTL V    -> V ExtractVelocityHalo(d, ts, ii, jj, kk)
         -> V DisplayVelocity(d, ts, ii, jj, kk)

; %{ return ((getPriority_t) getPriority)(COMPUTE_VELOCITY, ts, ii, jj, kk); %}

BODY
{

  ((computeVelocity_t) computeVelocity) (d, ts, ii, jj, kk);

}
END



ExtractVelocityHalo(d, ts, ii, jj, kk)

// Execution space
d  = 0 .. dim-1
ts = 2 .. nt-2 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
CTL V           <- V ComputeVelocity(d, ts, ii, jj, kk)
                -> V UpdateVelocity(d, ts, ii, jj, kk) /* Extraction must occur before Update to avoid RAW issues */

// TODO it is enough to just create the halo at the begining of the simulation and reuse the buffers for subsequent iterations
WRITE VRight    -> (ii < nxx-1) ? VLeft     UpdateVelocity(d, ts, ii+1, jj, kk) [type = VRIGHT_HALO]
WRITE VLeft     -> (ii > 0    ) ? VRight    UpdateVelocity(d, ts, ii-1, jj, kk) [type = VLEFT_HALO]

WRITE VForward  -> (jj < nyy-1) ? VBackward UpdateVelocity(d, ts, ii, jj+1, kk) [type = VFORWARD_HALO]
WRITE VBackward -> (jj > 0    ) ? VForward  UpdateVelocity(d, ts, ii, jj-1, kk) [type = VBACKWARD_HALO]

WRITE VTop      -> (kk < nzz-1) ? VBottom   UpdateVelocity(d, ts, ii, jj, kk+1) [type = VTOP_HALO]
WRITE VBottom   -> (kk > 0    ) ? VTop      UpdateVelocity(d, ts, ii, jj, kk-1) [type = VBOTTOM_HALO]

; %{ return ((getPriority_t) getPriority)(EXTRACT_VELOCITY_HALO, ts, ii, jj, kk); %}

BODY
{

  if (ii > 0){
    ((extractVelocityHalo_t) extractVelocityHalo) (LEFT, d, ts, ii, jj, kk, VLeft);
  }

  if (ii < nxx-1){
    ((extractVelocityHalo_t) extractVelocityHalo) (RIGHT, d, ts, ii, jj, kk, VRight);
  }

  if (jj > 0){
    ((extractVelocityHalo_t) extractVelocityHalo) (BACKWARD, d, ts, ii, jj, kk, VBackward);
  }

  if (jj < nyy-1){
    ((extractVelocityHalo_t) extractVelocityHalo) (FORWARD, d, ts, ii, jj, kk, VForward);
  }

  if (kk > 0){
    ((extractVelocityHalo_t) extractVelocityHalo) (BOTTOM, d, ts, ii, jj, kk, VBottom);
  }

  if (kk < nzz-1){
    ((extractVelocityHalo_t) extractVelocityHalo) (TOP, d, ts, ii, jj, kk, VTop);
  }

}
END



UpdateVelocity(d, ts, ii, jj, kk)

// Execution space
d  = 0 .. dim-1
ts = 2 .. nt-2 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
READ VLeft      <-  (ii > 0    ) ? VRight    ExtractVelocityHalo(d, ts, ii-1, jj, kk)   [type = VLEFT_HALO]
                <- !(ii > 0    ) ? NULL
READ VRight     <-  (ii < nxx-1) ? VLeft     ExtractVelocityHalo(d, ts, ii+1, jj, kk)   [type = VRIGHT_HALO]
                <- !(ii < nxx-1) ? NULL

READ VBackward  <-  (jj > 0    ) ? VForward  ExtractVelocityHalo(d, ts, ii, jj-1, kk)   [type = VBACKWARD_HALO]
                <- !(jj > 0    ) ? NULL
READ VForward   <-  (jj < nyy-1) ? VBackward ExtractVelocityHalo(d, ts, ii, jj+1, kk)   [type = VFORWARD_HALO]
                <- !(jj < nyy-1) ? NULL

READ VBottom    <-  (kk > 0    ) ? VTop      ExtractVelocityHalo(d, ts, ii, jj, kk-1)   [type = VBOTTOM_HALO]
                <- !(kk > 0    ) ? NULL
READ VTop       <-  (kk < nzz-1) ? VBottom   ExtractVelocityHalo(d, ts, ii, jj, kk+1)   [type = VTOP_HALO]
                <- !(kk < nzz-1) ? NULL


CTL V           <- V ExtractVelocityHalo(d, ts, ii, jj, kk)


CTL VxH         -> (d == X) ? VxH ComputeStress(XX, ts+1, ii, jj, kk)
                -> (d == X) ? VxH ComputeStress(YY, ts+1, ii, jj, kk)
                -> (d == X) ? VxH ComputeStress(ZZ, ts+1, ii, jj, kk)
                -> (d == X) ? VxH ComputeStress(XY, ts+1, ii, jj, kk)
                -> (d == X) ? VxH ComputeStress(XZ, ts+1, ii, jj, kk)

CTL VyH         -> (d == Y) ? VyH ComputeStress(XX, ts+1, ii, jj, kk)
                -> (d == Y) ? VyH ComputeStress(YY, ts+1, ii, jj, kk)
                -> (d == Y) ? VyH ComputeStress(ZZ, ts+1, ii, jj, kk)
                -> (d == Y) ? VyH ComputeStress(XY, ts+1, ii, jj, kk)
                -> (d == Y) ? VyH ComputeStress(YZ, ts+1, ii, jj, kk)

CTL VzH         -> (d == Z) ? VzH ComputeStress(XX, ts+1, ii, jj, kk)
                -> (d == Z) ? VzH ComputeStress(YY, ts+1, ii, jj, kk)
                -> (d == Z) ? VzH ComputeStress(ZZ, ts+1, ii, jj, kk)
                -> (d == Z) ? VzH ComputeStress(XZ, ts+1, ii, jj, kk)
                -> (d == Z) ? VzH ComputeStress(YZ, ts+1, ii, jj, kk)

; %{ return ((getPriority_t) getPriority)(UPDATE_VELOCITY, ts, ii, jj, kk); %}

BODY
{

  if (ii > 0){
    ((updateVelocity_t) updateVelocity) (LEFT, d, ts, ii, jj, kk, VLeft);
  }

  if (ii < nxx-1){
    ((updateVelocity_t) updateVelocity) (RIGHT, d, ts, ii, jj, kk, VRight);
  }

  if (jj > 0){
    ((updateVelocity_t) updateVelocity) (BACKWARD, d, ts, ii, jj, kk, VBackward);
  }

  if (jj < nyy-1){
    ((updateVelocity_t) updateVelocity) (FORWARD, d, ts, ii, jj, kk, VForward);
  }

  if (kk > 0){
    ((updateVelocity_t) updateVelocity) (BOTTOM, d, ts, ii, jj, kk, VBottom);
  }

  if (kk < nzz-1){
    ((updateVelocity_t) updateVelocity) (TOP, d, ts, ii, jj, kk, VTop);
  }

}
END




ComputeStress(sc, ts, ii, jj, kk)

// Execution space
sc = 0 .. nsc-1
ts = 3 .. nt-1 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
CTL VxH <- (sc == XX || sc == YY || sc == ZZ || sc == XY || sc == XZ) ? VxH UpdateVelocity(X, ts-1, ii, jj, kk)
CTL VyH <- (sc == XX || sc == YY || sc == ZZ || sc == XY || sc == YZ) ? VyH UpdateVelocity(Y, ts-1, ii, jj, kk)
CTL VzH <- (sc == XX || sc == YY || sc == ZZ || sc == XZ || sc == YZ) ? VzH UpdateVelocity(Z, ts-1, ii, jj, kk)

CTL S   -> (ts < nt-1) ? S ExtractStressHalo(sc, ts, ii, jj, kk)
        -> S DisplayStress(sc, ts, ii, jj, kk)

; %{ return ((getPriority_t) getPriority)(COMPUTE_STRESS, ts, ii, jj, kk); %}

BODY
{

  ((computeStress_t) computeStress) (sc, ts, ii, jj, kk);

}
END


ExtractStressHalo(sc, ts, ii, jj, kk)

// Execution space
sc = 0 .. nsc-1
ts = 3 .. nt-1 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
CTL   S         <- (ts < nt-1) ? S ComputeStress(sc, ts, ii, jj, kk)
                -> (ts < nt-1) ? S UpdateStress(sc, ts, ii, jj, kk) /* Extraction must occur before Update to avoid RAW issues */

WRITE SRight    ->  (ii < nxx-1 && ts < nt-1) ? SLeft     UpdateStress(sc, ts, ii+1, jj, kk) [type = SRIGHT_HALO]
WRITE SLeft     ->  (ii > 0     && ts < nt-1) ? SRight    UpdateStress(sc, ts, ii-1, jj, kk) [type = SLEFT_HALO]

WRITE SForward  ->  (jj < nyy-1 && ts < nt-1) ? SBackward UpdateStress(sc, ts, ii, jj+1, kk) [type = SFORWARD_HALO]
WRITE SBackward ->  (jj > 0     && ts < nt-1) ? SForward  UpdateStress(sc, ts, ii, jj-1, kk) [type = SBACKWARD_HALO]

WRITE STop      ->  (kk < nzz-1 && ts < nt-1) ? SBottom   UpdateStress(sc, ts, ii, jj, kk+1) [type = STOP_HALO]
WRITE SBottom   ->  (kk > 0     && ts < nt-1) ? STop      UpdateStress(sc, ts, ii, jj, kk-1) [type = SBOTTOM_HALO]

; %{ return ((getPriority_t) getPriority)(EXTRACT_STRESS_HALO, ts, ii, jj, kk); %}

BODY
{

  if (ii > 0 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (LEFT, sc, ts, ii, jj, kk, SLeft);
  }

  if (ii < nxx-1 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (RIGHT, sc, ts, ii, jj, kk, SRight);
  }

  if (jj > 0 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (BACKWARD, sc, ts, ii, jj, kk, SBackward);
  }

  if (jj < nyy-1 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (FORWARD, sc, ts, ii, jj, kk, SForward);
  }

  if (kk > 0 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (BOTTOM, sc, ts, ii, jj, kk, SBottom);
  }

  if (kk < nzz-1 && ts < nt-1){
    ((extractStressHalo_t) extractStressHalo) (TOP, sc, ts, ii, jj, kk, STop);
  }

}
END


UpdateStress(sc, ts, ii, jj, kk)

// Execution space
sc = 0 .. nsc-1
ts = 3 .. nt-1 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning
: ddesc(ii, jj, kk)


// Data flow
READ SLeft      <-  (ii > 0     && ts < nt-1) ? SRight    ExtractStressHalo(sc, ts, ii-1, jj, kk)   [type = SLEFT_HALO]
                <- !(ii > 0     && ts < nt-1) ? NULL
READ SRight     <-  (ii < nxx-1 && ts < nt-1) ? SLeft     ExtractStressHalo(sc, ts, ii+1, jj, kk)   [type = SRIGHT_HALO]
                <- !(ii < nxx-1 && ts < nt-1) ? NULL

READ SBackward  <-  (jj > 0     && ts < nt-1) ? SForward  ExtractStressHalo(sc, ts, ii, jj-1, kk)   [type = SBACKWARD_HALO]
                <- !(jj > 0     && ts < nt-1) ? NULL
READ SForward   <-  (jj < nyy-1 && ts < nt-1) ? SBackward ExtractStressHalo(sc, ts, ii, jj+1, kk)   [type = SFORWARD_HALO]
                <- !(jj < nyy-1 && ts < nt-1) ? NULL

READ SBottom    <-  (kk > 0     && ts < nt-1) ? STop      ExtractStressHalo(sc, ts, ii, jj, kk-1)   [type = SBOTTOM_HALO]
                <- !(kk > 0     && ts < nt-1) ? NULL
READ STop       <-  (kk < nzz-1 && ts < nt-1) ? SBottom   ExtractStressHalo(sc, ts, ii, jj, kk+1)   [type = STOP_HALO]
                <- !(kk < nzz-1 && ts < nt-1) ? NULL


CTL S           <- (ts < nt-1) ? S ExtractStressHalo(sc, ts, ii, jj, kk)


CTL SxxH        -> (sc == XX && ts < nt-1) ? SxxH ComputeVelocity(X..Z, ts+1, ii, jj, kk)
CTL SyyH        -> (sc == YY && ts < nt-1) ? SyyH ComputeVelocity(X..Z, ts+1, ii, jj, kk)
CTL SzzH        -> (sc == ZZ && ts < nt-1) ? SzzH ComputeVelocity(X..Z, ts+1, ii, jj, kk)
CTL SxyH        -> (sc == XY && ts < nt-1) ? SxyH ComputeVelocity(X, ts+1, ii, jj, kk)
                -> (sc == XY && ts < nt-1) ? SxyH ComputeVelocity(Y, ts+1, ii, jj, kk)
CTL SxzH        -> (sc == XZ && ts < nt-1) ? SxzH ComputeVelocity(X, ts+1, ii, jj, kk)
                -> (sc == XZ && ts < nt-1) ? SxzH ComputeVelocity(Z, ts+1, ii, jj, kk)
CTL SyzH        -> (sc == YZ && ts < nt-1) ? SyzH ComputeVelocity(Y, ts+1, ii, jj, kk)
                -> (sc == YZ && ts < nt-1) ? SyzH ComputeVelocity(Z, ts+1, ii, jj, kk)

; %{ return ((getPriority_t) getPriority)(UPDATE_STRESS, ts, ii, jj, kk); %}

BODY
{

  if (ii > 0 && ts < nt-1){
    ((updateStress_t) updateStress) (LEFT, sc, ts, ii, jj, kk, SLeft);
  }

  if (ii < nxx-1 && ts < nt-1){
    ((updateStress_t) updateStress) (RIGHT, sc, ts, ii, jj, kk, SRight);
  }

  if (jj > 0 && ts < nt-1){
    ((updateStress_t) updateStress) (BACKWARD, sc, ts, ii, jj, kk, SBackward);
  }

  if (jj < nyy-1 && ts < nt-1){
    ((updateStress_t) updateStress) (FORWARD, sc, ts, ii, jj, kk, SForward);
  }

  if (kk > 0 && ts < nt-1){
    ((updateStress_t) updateStress) (BOTTOM, sc, ts, ii, jj, kk, SBottom);
  }

  if (kk < nzz-1 && ts < nt-1){
    ((updateStress_t) updateStress) (TOP, sc, ts, ii, jj, kk, STop);
  }

}
END



DisplayVelocity(d, ts, ii, jj, kk)

// Execution space
d  = 0 .. dim-1
ts = 2 .. nt-2 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning (visualization is performed on the master process)
: ddesc(0, 0, 0)


CTL V    <- V ComputeVelocity(d, ts, ii, jj, kk)


// Priority
; %{ return ((getPriority_t) getPriority)(DISPLAY_VELOCITY, ts, ii, jj, kk); %}


BODY
{
#ifdef VISUALIZE_EXECUTION

  ((displayVelocity_t) displayVelocity) (d, ts, ii, jj, kk, 0);

#endif
}
END



DisplayStress(sc, ts, ii, jj, kk)

// Execution space
sc = 0 .. nsc-1
ts = 3 .. nt-1 .. 2
ii = 0 .. nxx-1
jj = 0 .. nyy-1
kk = 0 .. nzz-1


// Parallel partitioning (visualization is performed on the master process)
: ddesc(0, 0, 0)


CTL S    <- S ComputeStress(sc, ts, ii, jj, kk)


// Priority
; %{ return ((getPriority_t) getPriority)(DISPLAY_STRESS, ts, ii, jj, kk); %}


BODY
{
#ifdef VISUALIZE_EXECUTION

  ((displayStress_t) displayStress) (sc, ts, ii, jj, kk, 0);

#endif
}
END
